/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.jfrog.bintray" version "1.8.4"
    id "java-library"
    id "maven-publish"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.5.1"
    id "com.github.ben-manes.versions" version "0.27.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    jcenter()
}

ext {
    ktorVersion = "1.3.1"
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"

    // KTOR
    implementation "io.ktor:ktor-client-core-jvm:$ktorVersion"

    // LOGGING
    implementation "koriit.kotlin:slf4j-utils:0.6.0"

    // TESTING
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
    testImplementation "koriit.kotlin:slf4j-utils-logback:0.2.3"
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktorVersion"
    testImplementation "io.mockk:mockk:1.9.3"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs + [
                "-Xuse-experimental=io.ktor.util.KtorExperimentalAPI"
        ]
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.36.0"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags project.hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "Ktor Client Logging"
                description = "Logging feature for Ktor-Client"
                url = "https://github.com/Koriit/ktor-client-logging"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/ktor-client-logging/blob/master/LICENSE"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ["bintrayMaven"]

    pkg {
        repo = "kotlin"
        name = "ktor-client-logging"
        version {
            name = project.version
            released = new Date()
        }
    }
}

///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
